// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions: 0, 1, -1, x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    
    // zero bus
    And16(a = x, b = false, out = zero);

    // zx
    Mux16(a = x, b = zero, sel = zx, out = tempx);

    // nx
    Not16(in = tempx, out = notx);
    Mux16(a = tempx, b = notx, sel = nx, out = newx);

    // zy   
    Mux16(a = y, b = zero, sel = zy, out = tempy);

    // ny
    Not16(in = tempy, out = noty);
    Mux16(a = tempy, b = noty, sel = ny, out = newy);

    // sum or &                   
    //// sum
    Add16(a = newx, b = newy, out = sumxy);

    //// and
    And16(a = newx, b = newy, out = andxy);

    ////choose
    Mux16(a = andxy, b = sumxy, sel = f, out = tempout);

    // no
    Not16(in = tempout, out = ntempout);
    Mux16(a = tempout, b = ntempout, sel = no, out[15] = msb, out[0..7] = first, out[8..15] = second, out = out);

    // zr
    Or8Way(in = first, out = orfirst);
    Or8Way(in = second, out = orsecond);
    Or(a = orfirst, b = orsecond, out = azr);
    Not(in = azr, out = zr);     
    
    // ng
    And(a = msb, b = true, out = ng);
}
